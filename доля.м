структура РезультатТестовогоВипадку
  провалено логічне = ні
  повідомлення текст = ""
кінець

структура ТестовийВипадок
  назва текст
  сценарій Дія
  результат РезультатТестовогоВипадку
кінець

структура СтатистикаТестування
  успішно число = 0
  провалено число = 0
  усього число = 0
кінець

структура Доля
  тестові_випадки список<ТестовийВипадок>
кінець

дія ТестовийВипадок.провалити(повідомлення текст = "")
  я.результат = РезультатТестовогоВипадку(провалено=так, повідомлення=повідомлення)
кінець

дія ТестовийВипадок.вірно(твердження)
  якщо !твердження
    я.провалити("Твердження не є вірне")
  кінець
кінець

дія Доля.старт()
  кількість_випадків = я.тестові_випадки.довжина

  якщо кількість_випадків рівно 0
    друк("Не отримано жодних тестових випадків.")
    вернути пусто
  кінець

  друк("Отримано %(кількість_випадків) тестових випадків.")

  статистика = СтатистикаТестування()

  перебрати я.тестові_випадки як тестовий_випадок
    ;; На майбутнє: результат повинен бути вже заданий як значення за замовчуванням
    тестовий_випадок.результат = РезультатТестовогоВипадку()
    тестовий_випадок.сценарій(тестовий_випадок)

    якщо тестовий_випадок.результат.провалено
      якщо тестовий_випадок.результат.повідомлення.довжина не рівно 0
        друк("[ПРОВАЛЕНО] Тестовий випадок %(тестовий_випадок.назва) провалено із повідомленням:", тестовий_випадок.результат.повідомлення)
      інакше
        друк("[ПРОВАЛЕНО] Тестовий випадок %(тестовий_випадок.назва) провалено.")
      кінець

      статистика.провалено += 1
    інакше
      друк("[УСПІШНО] %(тестовий_випадок.назва)")

      статистика.успішно += 1
    кінець

    статистика.усього += 1
  кінець

  друк(
    "Статистика:",
    "усього: %(статистика.усього)",
    "провалено: %(статистика.провалено)",
    "успішно: %(статистика.успішно)"
  )
кінець

дати Доля
дати ТестовийВипадок
